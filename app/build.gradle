import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd-HH-mm-ss")
    df.setTimeZone(TimeZone.getTimeZone("Europe/Berlin"))
    return df.format(new Date())
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    
    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "org.zephyrsoft.trackworktime"
        minSdkVersion 14
        targetSdkVersion 24
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    // rename final APK files
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    "TrackWorkTime_${variant.versionName}_${buildTime()}_${variant.buildType.name}${variant.signingReady ? "" : "-unsigned"}.apk")
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'ch.acra:acra:4.9.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.darwinsys:hirondelle-date4j:1.5.1'
    compile 'com.getpebble:pebblekit:3.1.0@aar'
    compile 'net.sf.supercsv:super-csv:2.4.0'
    compile 'com.bignerdranch.android:recyclerview-multiselect:0.2'
}
